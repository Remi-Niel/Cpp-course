
%baseclass-preinclude "../rules/rules.h"

%token IDENT

// polymorphic so all the helper functions have nice signatures.
%polymorphic IDENT: std::string;

%type <IDENT> literal 
%type <IDENT> line

%%

ruleset:
    rule
    {
        print("\n");
    }
|
    ruleset rule
    {
        print("\n");
    }
;

rule:
    literal ':' rulebody ';'
    {
        set_lhs($1);
        print_completed();
    }
;

rulebody:
    line
    {
        append_rule($1);
    }
|
    rulebody '|' line
    {
        append_rule($3);
    }
;

line:
    literal
    {
        $$ = $1;
    }
|
    line literal
    {
        $$ = append($1, $2);
    }
;

literal:
    IDENT
    {
        $$ = d_scanner.matched();
    }
;


